<?xml version="1.0" encoding="utf-8"?>
<s:Application 
		xmlns:fx="http://ns.adobe.com/mxml/2009"
		xmlns:s="library://ns.adobe.com/flex/spark"
		xmlns:mx="library://ns.adobe.com/flex/mx"
		creationComplete="init()"
		width="400"
		height="140"
		backgroundColor="0x999999">

	<fx:Script>		
		<![CDATA[
			import mx.controls.Alert;
			import mx.core.FlexGlobals;

			private var streamer:String;
			private var file:String;
			private var camera:Camera;
			private var microphone:Microphone;
			private var connection:NetConnection;
			private var stream:NetStream;
			private var h264Settings:H264VideoStreamSettings;

			private function netStatusHander(event:NetStatusEvent):void {
				switch(event.info.code) {
					case 'NetConnection.Connect.Success':
						stream = new NetStream(connection);
						if(stream) {
							stream.attachCamera(camera);
							stream.attachAudio(microphone);

							h264Settings = new H264VideoStreamSettings();
							h264Settings.setProfileLevel(H264Profile.BASELINE, H264Level.LEVEL_1_2);
							stream.videoStreamSettings = h264Settings;
							stream.publish(file, 'live');				
						} else {
							Alert.show("Can't get NetStream.");
						}						
						break;
					case "NetConnection.Connect.Rejected":
					case "NetConnection.Connect.Closed":				
					case "NetConnection.Connect.Failed":						
						connection.connect(streamer);
						break;					
				}
			}

			private function setupMedia():void {
				microphone.setSilenceLevel(0);
				microphone.codec = "Speex";
				microphone.encodeQuality = 6;
				microphone.setUseEchoSuppression(true);
				
				camera.setMode(640, 480, 25);
				camera.setQuality(131072, 70);
				vd.attachCamera(camera);

				if(stream) {
					stream.attachCamera(camera);
					stream.attachAudio(microphone);
				}
			}
			
			private function init():void {
				streamer = FlexGlobals.topLevelApplication.parameters.server;
				file = FlexGlobals.topLevelApplication.parameters.stream;
				if(file == null) {
					Alert.show('Missing flashvars: stream');
					return;
				}
				if(streamer == null) {
					Alert.show('Missing flashvars: server');
					return;
				}

				camera = Camera.getCamera();
				microphone = Microphone.getMicrophone();
				setupMedia();
				
				connection = new NetConnection();
				connection.connect(streamer);
				connection.addEventListener(NetStatusEvent.NET_STATUS, netStatusHander);
			}

			public function onParamChange(): void
			{
				camera = Camera.getCamera(selectedCamera.selectedIndex.toString());
				microphone = Microphone.getMicrophone(selectedMicrophone.selectedIndex);
				setupMedia();
			}
			
		]]>
	</fx:Script>

	<mx:HBox>
		<mx:VideoDisplay id="vd" width="172" height="130">
		</mx:VideoDisplay>
		<mx:VBox width="200" verticalAlign="middle">
			<mx:Label text="Camera:"/>
			<mx:HBox>
				<mx:ComboBox id="selectedCamera" dataProvider="{Camera.names}" width="200" change="{onParamChange()}"/>
			</mx:HBox>	
			<mx:Label text="Microphone:"/>
			<mx:HBox>
				<mx:ComboBox id="selectedMicrophone" dataProvider="{Microphone.names}" width="200" change="{onParamChange()}"/>
			</mx:HBox>	
		</mx:VBox>
	</mx:HBox>
</s:Application>